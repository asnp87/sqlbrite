buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 26,
      'buildTools': '26.0.1',
      'kotlin': '1.1.50',
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.0.0-beta6'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  // Android dependencies.
  supportV4 = 'com.android.support:support-v4:26.1.0'
  supportAnnotations = 'com.android.support:support-annotations:26.1.0'
  supportTestRunner = 'com.android.support.test:runner:0.5'

  supportSqlite = 'android.arch.persistence.room:db:1.0.0-alpha9-1'
  supportSqliteFramework = 'android.arch.persistence.room:db-impl:1.0.0-alpha9-1'

  // Third-party dependencies.
  kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  dagger = 'com.google.dagger:dagger:2.11'
  daggerCompiler = 'com.google.dagger:dagger-compiler:2.11'
  butterKnifeRuntime = 'com.jakewharton:butterknife:8.8.1'
  butterKnifeCompiler = 'com.jakewharton:butterknife-compiler:8.8.1'
  timber = 'com.jakewharton.timber:timber:4.5.1'
  autoValue = 'com.google.auto.value:auto-value:1.5'
  autoValueParcel = 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
  rxJava = 'io.reactivex.rxjava2:rxjava:2.1.3'
  rxAndroid = 'io.reactivex.rxjava2:rxandroid:2.0.1'
  rxBinding = 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
  junit = 'junit:junit:4.12'
  truth = 'com.google.truth:truth:0.35'
  sqlcipher = 'net.zetetic:android-database-sqlcipher:3.5.7@aar'
}

configurations {
  osstrich
}
dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.2.0'
}
task publishV1Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite'
  ]
}
task publishV2Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite2'
  ]
}
task publishJavadoc(dependsOn: [publishV1Javadoc, publishV2Javadoc])
